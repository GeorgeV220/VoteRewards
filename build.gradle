buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'kr.entree.spigradle' version '2.3.4'
    id 'io.freefair.lombok' version '6.4.1'
    id 'java'
    id 'jacoco'
    id 'idea'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 17
targetCompatibility = 17

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")] as List<String>
    apiVersion = project.property("apiVersion")
    load = POSTWORLD
    libraries = ["org.mongodb:mongo-java-driver:3.12.10",
                 "mysql:mysql-connector-java:8.0.22",
                 "org.xerial:sqlite-jdbc:3.34.0",
                 "com.google.guava:guava:31.1-jre",
                 "org.postgresql:postgresql:42.2.18",
                 "commons-io:commons-io:2.11.0",
                 "commons-codec:commons-codec:1.15"]
    depends = ['Votifier']
    softDepends = ['PlaceholderAPI',
                   'ProtocolLib',
                   'WorldEdit',
                   'AuthMeReloaded',
                   'HolographicDisplays']
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

java {
    withSourcesJar()
    withJavadocJar()
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenLocal()
    mavenCentral()
    papermc()
    jitpack()
    bungeecord()
    codemc()
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${mcVersion}-R0.1-SNAPSHOT"

    implementation(bStats())
    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
    implementation 'com.github.GeorgeV220:API:2.3.1'
    implementation 'com.github.juliarn:npc-lib:2.6-RELEASE'

    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'fr.xephi:authme:5.6.0-SNAPSHOT'
    compileOnly 'com.github.NuVotifier.NuVotifier:nuvotifier-api:2.7.2'
    compileOnly 'com.github.NuVotifier.NuVotifier:nuvotifier-bukkit:2.7.2'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.4'
    compileOnly 'org.apache.commons:commons-lang3:3.12.0'
    compileOnly 'com.google.guava:guava:31.1-jre'
    compileOnly 'org.mongodb:mongo-java-driver:3.12.10'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.mockito:mockito-core:4.4.0'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.18:1.18.1'
    testImplementation 'org.assertj:assertj-core:3.22.0'
}

shadowJar {
    exclude 'com/cryptomorin/**'
    relocate 'co.aikar.commands', "${packageName}.acf"
    relocate 'co.aikar.locales', "${packageName}.locales"
    relocate 'org.bstats', 'com.georgev22.voterewards.bstats'
    relocate 'com.georgev22.api', 'com.georgev22.voterewards.api'
    relocate 'de.tr7zw.changeme.nbtapi', 'com.georgev22.voterewards.api.nbtapi'
    relocate 'de.tr7zw.annotations', 'com.georgev22.voterewards.api.annotations'
    relocate 'com.github.juliarn.npc', 'com.georgev22.voterewards.api.npc'
    relocate 'com.github.juliarn.relocation', 'com.georgev22.voterewards.api.npc'
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)
tasks.prepareSpigotPlugins.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    ignoreFailures = false
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'
